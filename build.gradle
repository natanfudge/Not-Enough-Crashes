plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle'
    id "com.modrinth.minotaur" version "2.+" apply false
    id 'io.github.juuxel.loom-quiltflower' version '1.6.0' apply false
}


architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: 'maven-publish'
    apply plugin: 'com.matthewprenger.cursegradle'
    apply plugin: "com.modrinth.minotaur"
    apply plugin: "io.github.juuxel.loom-quiltflower"

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.16.5+build.9:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    ext.total_version = "$mod_version+$minecraft_version"
    version = ext.total_version
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
        withSourcesJar()
    }

}

task publishAll {
    group = "upload"
    dependsOn("fabric:publishFabric")
    dependsOn("forge:publishForge")
}

task buildTestFabricMod(type: Exec) {
    workingDir "${project.projectDir}/TestFabricMod"
    commandLine "gradlew.bat", "build"
}

task addTestFabricMod(type: Copy) {
    dependsOn buildTestFabricMod
    from "TestFabricMod/build/libs/nec_testmod-1.0.0.jar"
    into "fabric/run/mods"
}

task buildTestForgeMod(type: Exec) {
    workingDir "${project.projectDir}/TestForgeMod"
    commandLine "gradlew.bat", "build"
}

task addTestForgeMod(type: Copy) {
    dependsOn buildTestForgeMod
//   Copy dev jar because there is no support for automatic remapping
    from "TestForgeMod/build/libs/nec_testmod-1.0.0-dev.jar"
    into "forge/run/mods"
}

task addTestMods {
    dependsOn addTestFabricMod, addTestForgeMod
}
